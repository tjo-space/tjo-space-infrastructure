default:
  @just --list

apply:
  #!/usr/bin/env sh
  cd {{source_directory()}}/terraform
  tofu init -upgrade
  tofu apply

destroy:
  #!/usr/bin/env sh
  cd {{source_directory()}}/terraform
  tofu  destroy

configure node tag="":
  #!/usr/bin/env sh
  cd {{source_directory()}}/ansible
  export VERSION=$(git describe --tags --always --dirty)
  TAGS=""; if [ -n "{{tag}}" ]; then TAGS="--tags {{tag}}"; fi
  ansible-playbook \
    --extra-vars "@{{source_directory()}}/../global.yaml" \
    --extra-vars "tjo_space_version=${VERSION}" \
    --inventory inventory.yaml \
    $TAGS \
    playbook.yaml --limit {{node}}

configure-all tag="":
  #!/usr/bin/env sh
  cd {{source_directory()}}/ansible
  export VERSION=$(git describe --tags --always --dirty)
  TAGS=""; if [ -n "{{tag}}" ]; then TAGS="--tags {{tag}}"; fi
  ansible-playbook \
    --extra-vars "@{{source_directory()}}/../global.yaml" \
    --extra-vars "tjo_space_version=${VERSION}" \
    --inventory inventory.yaml \
    $TAGS \
    playbook.yaml

ssh node:
  ssh bine@{{node}}.media.tjo.space -p 2222

build:
  #!/usr/bin/env sh
  cd {{source_directory()}}/build

  DATE=$(date -u +%Y\%m\%d)
  JELLYFIN_VERSION=10.11.6
  buildah build --file "jellyfin.Containerfile" \
    --build-arg VERSION=${JELLYFIN_VERSION} \
    --tag code.tjo.space/tjo-space/jellyfin:${JELLYFIN_VERSION}-${DATE} \
    .
  buildah push code.tjo.space/tjo-space/jellyfin:${JELLYFIN_VERSION}-${DATE}
